plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.3.0'
}

apply from: "$rootDir/version.gradle"

dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

group = 'com.github.amistad012oss.gradle'

// 配置 Gradle 插件发布（com.gradle.plugin-publish 1.0+ 新语法）
gradlePlugin {
    website = 'https://github.com/amistad012oss/gradle-git-repo-plugin'
    vcsUrl = 'https://github.com/amistad012oss/gradle-git-repo-plugin'
    plugins {
        gitRepoPlugin {
            id = 'com.github.amistad012oss.gradle.git-repo-plugin'
            implementationClass = 'com.github.amistad012oss.gradle.GitRepoPlugin'
            displayName = 'Gradle git repo plugin'
            description = 'This plugin allows you to add a git repository as a maven repo, even if the git repository is private, similar to how CocoaPods works.'
            tags.set(['git', 'repository', 'maven'])
        }
    }
}

// 配置 maven-publish 插件
publishing {
    publications {
        gitRepoPlugin(MavenPublication) {
            groupId "com.github.amistad012oss.gradle"
            artifactId 'git-repo-plugin'
            from components.java
        }
    }
    repositories {
        maven {
            url "file://$rootDir/releases"
        }
    }
}
